/*
 *  Copyright (C) 2017 José Roberto de Araújo Júnior
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

group 'org.platestack'
version = "0.1.0-SNAPSHOT"

buildscript {
    ext.kotlin_version = '1.1.2-4'
    ext.dokka_version = '0.9.13'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        //noinspection DifferentKotlinGradleVersion
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M4'
    }
}

subprojects {
    group = 'org.platestack'
    version = '0.1.0-SNAPSHOT'

    apply plugin: 'org.jetbrains.dokka'
    apply plugin: 'kotlin'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

        testCompile 'io.kotlintest:kotlintest:2.0.2'
        testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
        testCompile 'junit:junit:4.12'
    }

    task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
        outputFormat = "javadoc"
        outputDirectory = "$buildDir/javadoc"
        linkMapping {
            dir = "src/main/kotlin"
            url = "https://github.com/PlateStack/immutable-collections/blob/v0.1.0-alpha/src/main/kotlin"
            suffix = "#L"
        }
    }

    dokka {
        linkMapping {
            dir = "src/main/kotlin"
            url = "https://github.com/PlateStack/immutable-collections/blob/v0.1.0-alpha/src/main/kotlin"
            suffix = "#L"
        }
    }

    dokka.outputFormat = 'gfm'


}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'org.junit.platform.gradle.plugin'

repositories {
    mavenCentral()
}

configurations {
    compileUtil
    util {
        extendsFrom compileUtil
    }

    compile.extendsFrom util
}

sourceSets {
    util {
        java.srcDirs = []
        kotlin.srcDirs = ['src/util/kotlin']
        resources.srcDirs = ['src/util/resources']
        compileClasspath = configurations.compileUtil
    }
}

dependencies {
    compileUtil "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compileUtil "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compileUtil project(':plate-api:immutable-collections')

    util sourceSets.util.output

    compile sourceSets.util.output
    compile 'com.github.salomonbrys.kotson:kotson:2.5.0'
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'io.github.microutils:kotlin-logging:1.4.4'

    //testCompile 'io.kotlintest:kotlintest:2.0.2'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.jetbrains.spek:spek-api:1.1.1'
    testCompile 'org.junit.platform:junit-platform-runner:1.0.0-M4'
    testRuntime 'org.jetbrains.spek:spek-junit-platform-engine:1.1.1'
}

jar {
    from sourceSets.util.output
    from {
        ["CHANGELOG.md", "LICENSE.txt"]
    }
}

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

dokka {
    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/PlateStack/PlateAPI/blob/master/src/main/kotlin"
        suffix = "#L"
    }
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = "javadoc"
    outputDirectory = "$buildDir/javadoc"
}

task jarSources(type:Jar){
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jarSources
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
